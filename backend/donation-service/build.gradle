plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id "org.sonarqube" version "6.0.1.5171"
    id 'jacoco'
}

group = 'individual'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.postgresql:postgresql'
    implementation 'org.flywaydb:flyway-database-postgresql'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'com.fasterxml.jackson.core:jackson-databind'
    implementation("org.springframework.cloud:spring-cloud-starter-netflix-eureka-client:4.2.1")
    implementation 'org.springframework.boot:spring-boot-starter-amqp'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'io.micrometer:micrometer-registry-prometheus'
    implementation 'com.thoughtworks.xstream:xstream:1.4.21'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testRuntimeOnly 'com.h2database:h2'
}

tasks.named('test') {
    useJUnitPlatform()
}

sonar {
    properties {
        property "sonar.projectKey", "charity-fundraising-platform_donation-service"
        property "sonar.organization", "charity-fundraising-platform"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.token", System.getenv("SONAR_S6")
        property "sonar.coverage.exclusions", [
                "src/**/individual/donationservice/domain/**/*",
                "src/**/individual/donationservice/persistence/**/*",
                "src/**/individual/donationservice/external/**/*",
                "src/**/individual/donationservice/controller/**/*",
                "src/**/individual/donationservice/configuration/**/*",
                "src/**/individual/donationservice/messaging/**/*",
                "src/**/individual/donationservice/DonationServiceApplication.*"
        ]
    }
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    "individual/donationservice/domain/*",
                    "individual/donationservice/persistence/*",
                    "individual/donationservice/external/*",
                    "individual/donationservice/controller/*",
                    "individual/donationservice/configuration/*",
                    "src/**/individual/donationservice/messaging/*",
                    "individual/donationservice/DonationServiceApplication.class"
            ])
        }))
    }
}

tasks.named('sonar').configure {
    dependsOn jacocoTestReport
}

